import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
// Crashlytics依赖
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
//加载载入properties的gradle模块
apply from: "${rootProject.file('loadProperties.gradle')}"
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.ryuunoakaihitomi.rebootmenu"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1077004
        versionName "11.004"
        buildConfigField("String", "APK_PACK_TIME", "\"" + new SimpleDateFormat("yyyy-MM-dd HH:mm:ss:SSS").format(new Date()) + "\"")

        //导入API Key
        def adAppIdKey = loadProps('custom_config').getProperty('admobAdAppId')
        manifestPlaceholders = [AD_APP_ID: adAppIdKey]
        buildConfigField('String', 'AD_APP_ID', '\"' + adAppIdKey + '\"')
        buildConfigField('String', 'AD_INTERSTITIAL_UNIT', '\"' + loadProps('custom_config').getProperty('admobInterstitialAdUnit') + '\"')
        buildConfigField('String', 'AD_VIEW_UNIT', '\"' + loadProps('custom_config').getProperty('admobAdViewUnit') + '\"')
        buildConfigField('String', 'GITHUB_PAT', '\"' + loadProps('custom_config').getProperty('githubPAT') + '\"')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    //存放方法签名只包含纯java部分不包含Android部分的Android隐私API
    //这样就会避免在APK中留下不必要的API存根
    compileOnly project(':libandroidhiddenapi')
    //compileOnly引入androidx注解只能在代码高亮中出现提示，在发布编译的时候一样会找不到API
    //不再需要，以下第三方库已经包含了这些东西
    //implementation 'androidx.annotation:annotation:1.1.0-alpha01'

    //--第三方库
    //用来统计和上传崩溃报告
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    //用来显示广告
    implementation 'com.google.firebase:firebase-ads:17.2.0'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${variant.applicationId}-${variant.versionCode}_${variant.buildType.name}.apk"
    }
}
