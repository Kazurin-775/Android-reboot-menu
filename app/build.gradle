apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
/* 崩溃分析插件 */
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.ryuunoakaihitomi.rebootmenu"
        minSdkVersion 22
        // 以下方式会出现这样的错误：android:targetSdkVersion="android-30"
        //targetSdkVersion compileSdkVersion
        targetSdkVersion 30
        // 版本号：发布日期
        versionCode 20201121
        // 版本名说明： R3.x.y (R3:Refactoring 第三次重构)
        versionName "R3.4.0"
        resConfigs "en", "zh-rCN"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true  // TODO 此值发布时必须为true
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // ZXing需要method sort(Ljava/util/Comparator;)V in class Ljava/util/List;
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    /* 崩溃报告组件 */
    implementation platform('com.google.firebase:firebase-bom:26.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation 'github.ryuunoakaihitomi.poweract:poweract:1.2.6'
    implementation 'com.github.GrenderG:Toasty:1.5.0'
    implementation "com.github.topjohnwu.libsu:core:3.0.2"
    implementation 'moe.shizuku.privilege:api:4.2.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    implementation 'commons-io:commons-io:2.8.0'
    final def markwon_version = '4.6.0'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation 'github.ryuunoakaihitomi.retoast:retoast:1.0.2'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.drakeet.about:about:2.4.1'
    debugImplementation "com.github.whataa:pandora:androidx_v2.1.0"
    implementation 'com.google.zxing:core:3.4.1'
}

// 规格化APK文件名
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionCode}_${variant.versionName}_${variant.buildType.name}.apk"
    }
}
repositories {
    mavenCentral()
}
