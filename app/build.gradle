import java.time.LocalDateTime

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.huawei.agconnect'

apply from: "${rootProject.file('loadProperties.gradle')}"

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.ryuunoakaihitomi.rebootmenu"
        minSdkVersion 19
        multiDexEnabled true
        // 以下方式会出现这样的错误：android:targetSdkVersion="android-30"
        //targetSdkVersion compileSdkVersion
        targetSdkVersion 31
        // 版本号：发布日期
        versionCode 20210907
        // 版本名说明： R3.x.y.z (R3:Refactoring 第三次重构，z:Quick Fix序号)
        versionName 'R3.12.1'
        resConfigs "en", "zh-rCN"

        buildConfigField 'String', 'BUILD_TIME', '\"' + new Date() + '\"'
    }

    signingConfigs {
        release {
            Properties p = loadProps('signInfo')
            keyAlias p['KEY_ALIAS']
            keyPassword p['KEY_PWD']
            storePassword p['STORE_PWD']
            storeFile file(p['STORE_FILE'])
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true  // TODO 此值发布时必须为true
            }
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix '-debug' +/* 精确判断是不是刚才编译的debug版本 */LocalDateTime.now().format("(dHHmmss)")
        }
    }
    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
        }
        // Free/Libre and Open Source Software
        floss {
            dimension "version"
            versionNameSuffix '-floss'
            applicationId 'github.ryuunoakaihitomi.powerpanel'  // 现在想要个新的packageName
            // 华为插件不知道加的什么东西
            // https://stackoverflow.com/a/30497221/16091156
            aaptOptions {
                ignoreAssetsPattern '!tmp_json.html'
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        textReport true

        // 实测看上去没有区别
        // Resource references will not work correctly in images generated for this vector icon for API < 21; check generated icon to make sure it looks acceptable
        disable 'VectorRaster'
    }
    // https://android-developers.googleblog.com/2020/05/android-studio-4.html
    // Dependencies metadata 章节
    dependenciesInfo.includeInApk = false
    packagingOptions {
        exclude 'META-INF/*.version'     // 移除记录依赖的版本信息文件（大部分是AndroidX的）
        exclude 'androidsupportmultidexversion.txt'
        // https://stackoverflow.com/a/41073782/16091156
        exclude 'kotlin/**'
        // https://github.com/Kotlin/kotlinx.coroutines/issues/2274
        exclude 'DebugProbesKt.bin'
        // 移除Normal构建中的G组件版本
        // https://stackoverflow.com/a/58855506/16091156
        exclude 'firebase-*.properties'
        exclude 'play-services-*.properties'
        exclude 'transport-*.properties'
        // 华为AGC插件
        exclude 'agconnect-*.properties'
        exclude 'HMSCore-*.properties'
        exclude 'network-*.properties'
    }
}

dependencies {
    // ZXing需要method sort(Ljava/util/Comparator;)V in class Ljava/util/List;
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    normalImplementation platform('com.google.firebase:firebase-bom:28.3.1')
    normalImplementation 'com.google.firebase:firebase-analytics-ktx'
    normalImplementation 'com.google.firebase:firebase-crashlytics-ktx'

    normalImplementation 'com.huawei.hms:hianalytics:6.2.0.301'

    /* ------- 其他第三方开源组件 ------- */

    final def markwon_version = '4.6.2'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:image:$markwon_version"

    def shizuku_version = '12.0.0'
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    implementation 'github.ryuunoakaihitomi.poweract:poweract:1.5.3'
    runtimeOnly 'github.ryuunoakaihitomi.retoast:retoast:1.7.4'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.github.GrenderG:Toasty:1.5.2'
    implementation 'com.github.topjohnwu.libsu:core:3.1.2'
    // 固定在此版本，理由同下段注释
    // java.lang.NoClassDefFoundError: j$.wrappers.$r8$wrapper$java$util$function$Supplier$-WRP
    // 目前搜集到的一些资料
    // https://issuetracker.google.com/issues/183419297
    // https://issuetracker.google.com/issues/157854693
    // https://issuetracker.google.com/issues/155005951
    // https://issuetracker.google.com/issues/190633902
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.8.0'
    // 不再使用lang3，因为lang3使用的一些java8 API是脱糖库无法完成转换的，即使脱糖库的文档说明支持。
    // 使用到的例子：
    //  FieldUtils#getField(Class<?>, String, boolean)
    //  Validate#notNull(T, String, Object...)
    //  Objects#requireNonNull(T, Supplier<String>) <- 从这里开始，java.util.function.Supplier无法脱糖成功，而24以下环境找不到它
    // FIXME 在没有找到更好的解决方法之前只能使用legacy lang2代替
    //implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'org.cyanogenmod:platform.sdk:6.0'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:2.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    // 最新版是v2.1.0，仅支持android-support
    //noinspection GradleDependency
    debugImplementation 'com.github.whataa:pandora:androidx_v2.1.0'
}

android.applicationVariants.all { variant ->

    // 规格化APK文件名
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionCode}_${variant.versionName}_${variant.buildType.name}.apk"
    }

    // 在floss变体中过滤gms插件任务
    // processFlossReleaseGoogleServices
    tasks.forEach { task ->
        final def name = task.name
        if (['Google', 'Crashlytics'].any { e -> name.contains(e) }) {
            logger.lifecycle "G task in ${variant.flavorName} -> $name"
            if (name.contains('floss'.capitalize())) {
                System.err.println "Skip G task for floss: $name"
                task.enabled = false
            }
        }
    }
}

repositories {
    maven {
        url 'https://raw.githubusercontent.com/ryuunoakaihitomi/maven-repository/master'
    }
}