apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
// 崩溃分析组件所用插件，若想保护隐私可以注释掉
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.ryuunoakaihitomi.rebootmenu"
        minSdkVersion 22
        targetSdkVersion 29
        // 版本号：发布日期
        versionCode 20200817
        /*
         版本名说明：
         R3.0.0
         Refactoring 第三次重构+功能更新计数+缺陷修复计数
         */
        versionName "R3.0.0"
        resConfigs "en", "zh-rCN"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    /* 崩溃报告组件，若想保护隐私可以注释掉 */
    implementation 'com.google.firebase:firebase-analytics-ktx:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    /* --- 使用的非Google提供开源库 --- */
    // PowerAct
    implementation 'github.ryuunoakaihitomi.poweract:poweract:1.1.0'
    // Toasty
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    /* libsu */
    def libsuVersion = '3.0.2'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    // Apache Commons IO
    implementation 'commons-io:commons-io:2.7'
    // Markwon
    final def markwon_version = '4.5.1'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
}

// 规格化APK文件名
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionCode}_${variant.versionName}_${variant.buildType.name}.apk"
    }
}
