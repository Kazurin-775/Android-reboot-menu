apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
/* 崩溃分析插件 */
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.ryuunoakaihitomi.rebootmenu"
        minSdkVersion 22
        // 以下方式会出现这样的错误：android:targetSdkVersion="android-30"
        //targetSdkVersion compileSdkVersion
        targetSdkVersion 30
        // 版本号：发布日期
        versionCode 20201028
        /*
         版本名说明：
         R3.0.0
         Refactoring 第三次重构+功能更新+缺陷修复
         */
        versionName "R3.3.2"
        resConfigs "en", "zh-rCN"
        // 致用户：可以在这里禁用Firebase，将以下值设为true
        final def disableFirebase = false
        buildConfigField('boolean', 'DISABLE_FIREBASE', String.valueOf(disableFirebase))
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true  // TODO 此值发布时必须为true
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    /* 崩溃报告组件 */
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    /* --- 使用的非Google提供开源库 --- */
    implementation 'github.ryuunoakaihitomi.poweract:poweract:1.2.5'
    implementation 'com.github.GrenderG:Toasty:1.5.0'
    implementation "com.github.topjohnwu.libsu:core:3.0.2"
    implementation 'moe.shizuku.privilege:api:4.2.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    implementation 'commons-io:commons-io:2.8.0'
    final def markwon_version = '4.6.0'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation 'github.ryuunoakaihitomi.retoast:retoast:1.0.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.drakeet.about:about:2.4.1'
}

// 规格化APK文件名
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionCode}_${variant.versionName}_${variant.buildType.name}.apk"
    }
}
repositories {
    mavenCentral()
}
